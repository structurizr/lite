plugins {
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'java'
	id 'jvm-test-suite'
	id 'war'
}

group = 'com.structurizr'

sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
	mavenCentral()
    mavenLocal()
}

testing {
	suites {
		test {
			useJUnitJupiter()
		}

		integrationTest(JvmTestSuite) {
			dependencies {
				implementation project()
			}

			targets {
				all {
					testTask.configure {
						shouldRunAfter(test)
					}
				}
			}
		}
	}
}

tasks.named('check') {
	dependsOn(testing.suites.integrationTest)
}

ext {
	set('snakeyaml.version','2.0')
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:10.1.31'
	implementation 'jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api:3.0.0'
	implementation 'org.glassfish.web:jakarta.servlet.jsp.jstl:3.0.1'

	def structurizrVersion = '3.2.0'
	implementation "com.structurizr:structurizr-dsl:${structurizrVersion}"
	implementation "com.structurizr:structurizr-autolayout:${structurizrVersion}"
	implementation "com.structurizr:structurizr-inspection:${structurizrVersion}"

	implementation 'org.codehaus.groovy:groovy-jsr223:3.0.22'
	implementation 'org.jetbrains.kotlin:kotlin-scripting-jsr223:1.9.25'
	implementation 'org.jruby:jruby-core:9.4.8.0'

	def luceneVersion = '9.12.0'
	implementation "org.apache.lucene:lucene-core:${luceneVersion}"
	implementation "org.apache.lucene:lucene-queryparser:${luceneVersion}"
}

bootWar {
	requiresUnpack '**/kotlin-*.jar'
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

configurations.all {
	exclude group: "commons-logging", module: "commons-logging"
}